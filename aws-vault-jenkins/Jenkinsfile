import hudson.util.Secret
import com.cloudbees.plugins.credentials.CredentialsScope
import com.datapipe.jenkins.vault.credentials.VaultTokenCredential

def vault_cred_id = 'vault-jenkins-auth'
def vault_addr = 'http://54.165.198.223:8200'

pipeline {
    agent any

    stages {
        stage('Get a Vault token for the pipeline') {
            steps {
                withCredentials([
                    [
                        $class: 'VaultTokenCredentialBinding',
                        credentialsId: vault_cred_id,
                        vaultAddr: vault_addr
                    ]
                ]){
                    script {
                        // Get wrapped secret id
                        wrapped_secret_response = httpRequest(
                            quiet: true,
                            url: env.VAULT_ADDR + '/v1/auth/pipeline/role/pipeline-role/secret-id',
                            httpMode: 'POST',
                            customHeaders: [
                                [name:'X-Vault-Token', value: env.VAULT_TOKEN],
                                [name:'X-Vault-Wrap-Ttl', value:'300s']
                            ],
                            consoleLogResponseBody: false
                        )
                        def WRAPPED_SECRET_ID = readJSON(text: wrapped_secret_response.content).wrap_info.token

                        // Unwrap the secret id
                        String unwrap_request_body = writeJSON returnText: true, json: ['token': WRAPPED_SECRET_ID]

                        unwrap_secret_response = httpRequest(
                            quiet: true,
                            url: env.VAULT_ADDR + '/v1/sys/wrapping/unwrap',
                            httpMode: 'PUT',
                            customHeaders: [
                                [name:'X-Vault-Token', value: env.VAULT_TOKEN],
                                [name:'X-Vault-Request', value:'true']
                            ],
                            consoleLogResponseBody: false,
                            requestBody: unwrap_request_body
                        )

                        // If unwrapping request fails, send notification...
                        if (unwrap_secret_response.status != 200) {
                            currentBuild.result = 'FAILURE'
                            notifyFailed()
                            error('Error unwrapping the secret id.')
                        }

                        def SECRET_ID = readJSON(text: unwrap_secret_response.content).data.secret_id

                        withCredentials([string(credentialsId: 'pipeline-role-id', variable: 'ROLE_ID')]) {    
                            // Use role-id and secret-id to get a token
                            pipeline_token_response = httpRequest(
                                quiet: true,
                                url: env.VAULT_ADDR + '/v1/auth/pipeline/login',
                                httpMode: 'PUT',
                                customHeaders: [
                                    [name:'X-Vault-Token', value: env.VAULT_TOKEN],
                                    [name:'X-Vault-Request', value:'true']
                                ],
                                requestBody: writeJSON(returnText: true, json: [
                                    'role_id': ROLE_ID, 'secret_id': SECRET_ID
                                ]),
                                consoleLogResponseBody: false
                            )
                            env.PIPELINE_VAULT_TOKEN = readJSON(text: pipeline_token_response.content).auth.client_token
                        }
                    }
                }
            }
        }

        stage('Build app infrastructure with Terraform') {
            environment {
                TF_VAR_aws_region = 'us-east-1'
                TF_VAR_key_name = 'vault-kp'
                TF_VAR_tpl_vault_server_addr = "${vault_addr}"
            }
            steps {
                script {
                    VaultTokenCredential pipelineCredential = new VaultTokenCredential(
                        CredentialsScope.GLOBAL,
                        'custom-credential',
                        'My Custom Credential',
                        Secret.fromString("${env.PIPELINE_VAULT_TOKEN}")
                    )
                    def configuration = [vaultUrl: vault_addr, engineVersion: 1,
                            vaultCredential: pipelineCredential]
                    def secrets = [
                        [path: 'db-creds/db', engineVersion: 1, secretValues: [
                            [envVar: 'TF_VAR_username', vaultKey: 'user'],
                            [envVar: 'TF_VAR_password', vaultKey: 'password']]],
                        [path: 'aws/creds/pipeline-role', engineVersion: 1, secretValues: [
                            [envVar: 'AWS_ACCESS_KEY_ID', vaultKey: 'access_key'],
                            [envVar: 'AWS_SECRET_ACCESS_KEY', vaultKey: 'secret_key']]]
                    ]
                    withVault([configuration: configuration, vaultSecrets: secrets]) {
                        
                        git branch: 'main', url: 'https://github.com/andregri/terraform-examples.git'
                        
                        sh("""
                            cd "${WORKSPACE}/aws-vault-jenkins/app"
                            terraform init -no-color
                            terraform apply -auto-approve -no-color
                            terraform destroy -auto-approve -no-color
                        """)
                    }
                }
            }
        }
    }
}

def notifyFailed() {
    emailext (
        subject: "FAILED: Job '${env.JOB_NAME} [${env.BUILD_NUMBER}]'",
        body: """<p>FAILED: Job '${env.JOB_NAME} [${env.BUILD_NUMBER}]':</p>
            <p>Check console output at &QUOT;<a href='${env.BUILD_URL}'>${env.JOB_NAME} [${env.BUILD_NUMBER}]</a>&QUOT;</p>""",
        recipientProviders: [[$class: 'DevelopersRecipientProvider']]
    )
}